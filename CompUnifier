//desc: Gain unity receiver
import basic-util.jsfx-inc
import sample-measure.jsfx-inc
import loudness-util.jsfx-inc

in_pin: Left input
in_pin: Right input
out_pin: Left output
out_pin: Right output

slider1:50<0.1,100,0.1>Sample duration (ms)
slider2:1<0,1,1{Done,Learning}>Learnning phase mode
slider3:6<0,12,0.1>Attack
slider4:6<0,12,0.1>Sustain
slider5:-27<-72,0,0.1>Target volume
slider6:0<-24,24,0.1>Output volume
slider7:100<0,100,1>Mix

@init
ext_noinit = 1.0;
MINIMUM_VOLUME = 72;

function drawVolume(dataArray, column, r, g, b, a) local(xStart, xEnd, yStart, yEnd, dataIndex, factorColor) (
  xStart = column-1;
  xEnd = column;
  
  dataIndex = pixelMemoryIterator - (gfx_w-column);
  dataIndex < 2 ? dataIndex += gfx_w;
  
  yStart = dataArray[dataIndex-1];
  yEnd = dataArray[dataIndex];

  yStart <= 0 ? yStart = gfx_h;
  yEnd <= 0 ? yEnd = gfx_h;
  
  factorColor = .4;
  gfx_r = r * factorColor * a;
  gfx_g = g * factorColor * a;
  gfx_b = b * factorColor * a;
  gfx_line(xEnd-1, yEnd+1, xEnd-1, gfx_h);
  
  gfx_r = r * a;
  gfx_g = g * a;
  gfx_b = b * a;
  gfx_line(xStart, yStart, xEnd, yEnd);
  gfx_line(xStart, yStart+1, xEnd, yEnd+1);
);

function drawVisualizer() local(column) (
  column = gfx_w;
    
  loop(gfx_w,
    column > 0 ? drawVolume(gfxPixelsSample, column, 1, .5, .5, 1);
    column -= 1;
  );
);


function setGainSliderValue(ratio) (
  slider6 = round(convertRatioToDbGain(ratio), 1);
);

function getAppliedRatio() (
    ((100-slider7)/100) * 1 + (slider7/100) * this.ratio;
);

function releaseAttackRatio() (
    this.oldAttackRatio = (2 + this.oldAttackRatio) / 3;
);
function releaseSustainRatio() (
    this.oldSustainRatio = (2 + this.oldSustainRatio) / 3;
);
function setNewRatio(oldRatio, newRatio) (
    this.ratio = (2*oldRatio + newRatio) / 3;
    //this.ratio = newRatio;
);

function computeRatio(measureVolume) local(newRatio) (
    volumeDb = convertRmsToDb(measureVolume);
    oldRatio = this.ratio;
    newRatio = targetVolume / measureVolume;
  
    volumeDb > targetVolumeDb ? (
        volumeDb < rangeUpper ? ( //limit attack
            compAttack +=1;
            this.setNewRatio(this.oldAttackRatio, newRatio);
            this.oldAttackRatio = newRatio;
        ) : ( //too much attack
            compAttackOutRange +=1;
            this.ratio = this.oldAttackRatio; //keep the higher attack ratio
        );
        
        this.releaseSustainRatio();
    );
    
    volumeDb < targetVolumeDb ? (
        volumeDb > rangeLower ? ( //add sustain
            compSustain +=1;
            this.setNewRatio(this.oldSustainRatio, newRatio);
            this.oldSustainRatio = newRatio;
        ) : volumeDb > gateDb ? ( //not enough sustain
            compSustainOutRange +=1;
            this.ratio = this.oldSustainRatio; //keep the higher sustain ratio
        ) : ( //let the volume rest in peace for silence moments
            compSilent += 1;
            this.releaseSustainRatio();
            this.ratio = 1;
        );
        
        this.releaseAttackRatio();
    );

    //this.ratio = (2*this.ratio + newRatio) / 3;
    //this.ratio = newRatio;
    setGainSliderValue(this.getAppliedRatio());
    measure.resetSample();
);

function setTargetVolume(newVolume) (
  slider5 != newVolume ? slider5 = round(newVolume, 1);
  targetVolume = convertDbLevelToRatio(newVolume);
  targetVolumeDb = newVolume;
  rangeUpper = newVolume + slider3;
  rangeLower = newVolume - slider4;
  gateDb = rangeLower - slider4;
);


//Variables initialization
learnDuration = srate;
oldMode = slider2;
measure.resetSample();
setTargetVolume(slider5);
unifier.oldAttackRatio = 1;
unifier.oldSustainRatio = 1;

gfxPixelsSample = createArrayAndInit(gfx_w, -1);
//gfxPixelsResult = createArrayAndInit(gfx_w, -1);

@slider
learnDuration = slider1 / 1000 * srate;
setTargetVolume(slider5);

 
@block
measure.count >= learnDuration ? (
  unifier.computeRatio(measure.average);
);


@sample
sample.getSample(spl0, spl1);

measure.learnNewLevel(sample.getAbsoluteVolume());
slider2 == 1 ? (
  oldMode == 0 ? (measureIntegration.resetSample(); oldMode=1);
  
  setTargetVolume(convertRmsToDb(measureIntegration.average));
  measureIntegration.learnNewLevel(sample.getAbsoluteVolume());
);
oldMode = slider2;


result.getSample(unifier.getAppliedRatio() * sample.left, unifier.getAppliedRatio() * sample.right);
resultDb = convertRmsToDb(result.getAbsoluteVolume());

timeCounter == srate/100 ? (
  gfxPixelsSample[pixelMemoryIterator] = pixelSampleMemory / timeCounter;
  //gfxPixelsResult[pixelMemoryIterator] = pixelResultMemory / timeCounter;
  pixelSampleMemory = 0;
  pixelResultMemory = 0;
  pixelMemoryIterator += 1;
  pixelMemoryIterator == gfx_w ? pixelMemoryIterator = 1;
  timeCounter = 0;
);

pixelSampleMemory += spl0 != 0 ? -volumeDb / MINIMUM_VOLUME * gfx_h : -1;
//pixelResultMemory += spl0 != 0 ? -resultDb / MINIMUM_VOLUME * gfx_h : -1;
timeCounter += 1;

spl0 = result.left;
spl1 = result.right;



@gfx 300 600
heightTarget = -targetVolumeDb / MINIMUM_VOLUME * gfx_h;
heightUpper = -(rangeUpper) / MINIMUM_VOLUME * gfx_h;
heightLower = -(rangeLower) / MINIMUM_VOLUME * gfx_h;
heightGate = -(gateDb) / MINIMUM_VOLUME * gfx_h;

backgroundColor = .12;
gfx_r=backgroundColor -.05;
gfx_g=backgroundColor -.00;
gfx_b=backgroundColor +.05;

gfx_rect(0, 0, gfx_w, gfx_h);

drawVisualizer();

//range limits
gfx_r=.6;
gfx_g=.6;
gfx_b=0;
gfx_line(0, heightUpper, gfx_w, heightUpper);
gfx_line(0, heightLower, gfx_w, heightLower);

gfx_x=gfx_y=0;
gfx_blurto(gfx_w, gfx_h);

//gate
gfx_r=.6;
gfx_g=0;
gfx_b=0;
gfx_line(0, heightGate, gfx_w, heightGate);

//target
gfx_r=0;
gfx_g=.9;
gfx_b=0;
gfx_line(0, heightTarget, gfx_w, heightTarget);
